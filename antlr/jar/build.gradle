plugins {
    id 'java-library'
    id 'antlr'
}

repositories {
    mavenCentral()
}

def antlrPackage = 'com.tedneward.lolcode'
def antlrPackagePath = antlrPackage.replaceAll('\\.', '/')
def antlrPackageFile = file("${projectDir}/src/main/java/${antlrPackagePath}")

dependencies {
    antlr 'org.antlr:antlr4:4.10.1'
    implementation 'org.antlr:antlr4-runtime:4.10.1'

    implementation 'com.google.guava:guava:31.0.1-jre'

    api 'org.apache.commons:commons-math3:3.6.1'
}

// Antlr stuff
//
tasks.register('copyGrammarSource', Copy) {
    from '../lolcode.g4'
    into 'src/main/antlr'
}
// Standard ANTLR block
generateGrammarSource {
    maxHeapSize = "64m"
    arguments += [ "-visitor",
                   "-long-messages",
                   "-package", antlrPackage
                   ]
    outputDirectory = antlrPackageFile

    dependsOn copyGrammarSource
}
compileJava {
    dependsOn generateGrammarSource
}
clean {
    delete "${projectDir}/src/main/java"
    delete "${projectDir}/src/main/antlr"
}
